global groups, statistics

// define 4 groups of interesting events: networking, io, process and portscan
probe group.networking = syscall.socket, syscall.socketpair 
{   
    groupname = "networking"    
}

probe group.io = syscall.open, syscall.close, 
            syscall.read, syscall.write
{   
    groupname = "io"    
}

probe group.process = syscall.fork, syscall.execve
{   
    groupname = "process"   
}

probe group.portscan = portscan.tcpconnect, portscan.nullfinxmas
{
    groupname = "portscan"
}

// keep track of events
probe group.* {
    groups[groupname]++
}

// update statistics every second
probe timer.ms(1000) {
    foreach (eg in groups) 
        statistics[eg] <<< groups[eg]

    delete groups
}

function print_avg(groupname) {
    return (groupname in statistics) ? @avg(statistics[groupname]) : 0;
}

// every 10 seconds print average values
probe timer.ms(10000)
{
    printf("%5d %5d %5d %5d\n", print_avg("networking"),
                                print_avg("process"),
                                print_avg("io"),
                                print_avg("portscan"));
    delete statistics
}
